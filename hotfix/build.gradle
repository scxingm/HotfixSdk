apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
            zipAlignEnabled false
            proguardFiles 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:26.+'
}

task clearJar(type: Delete) {
    delete "libs/hotfix-debug.jar"
    delete "libs/hotfix-release.jar"
}

task makeJar(dependsOn: ['clearJar'], type: Jar){

    //从哪个目录下生成
    from file('build/intermediates/classes/release')
    //生成的名字
    archiveName = "hotfix-debug.jar"
    //生成的目录
    destinationDir = file('libs')
    //过滤不需要的class，防止使用的时候出现dex错误
    exclude "**/**/BuildConfig.class"
    exclude "**/**/BuildConfig\$*.class"
    exclude "**/R.class"
    exclude "**/R\$*.class"
    include "com/android/internal/util/**/*.class"
    include "cn/scxingm/hotfix/**/*.class"
}
makeJar.dependsOn(build)

task makePro(dependsOn: ['makeJar'], type: proguard.gradle.ProGuardTask) {
    //输入的jar路径
    injars "libs/hotfix-debug.jar"
    //输出的jar路径
    outjars "libs/hotfix-release.jar"
    //混淆配置
    configuration 'proguard-rules.pro'
}

task makeHotfixJar(dependsOn: ['makePro'], type: cn.scxingm.plugin.hotfix.Hotfix){
    doLast{
        makeHotfixJar()
    }
}